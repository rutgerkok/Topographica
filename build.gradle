import org.apache.tools.ant.filters.ReplaceTokens

defaultTasks "spotlessApply", 'build'

allprojects {
    version = "0.0.1-SNAPSHOT"
    group = "nl.rutgerkok.topographica"
}

buildscript {
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" } // For Spotless
        jcenter() // For Shadow
    }
    dependencies {
        classpath "com.diffplug.spotless:spotless-plugin-gradle:3.23.0"
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.0.0'
    }
}

subprojects {

    repositories {
        mavenCentral()
        maven {
            url 'https://hub.spigotmc.org/nexus/content/groups/public/' // Spigot repo
        }
    }

    apply plugin: "java"
    apply plugin: "com.diffplug.gradle.spotless"

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    spotless { // Code formatting settings
        java {
            removeUnusedImports()
            importOrderFile rootProject.file('misc/eclipse-importorder-settings.importorder')
            trimTrailingWhitespace()
            endWithNewline()
            eclipse().configFile rootProject.file('misc/eclipse-formatting-settings.xml')
            paddedCell()
        }
    }

    processResources { // Web, config.yml and plugin.yml replacements
        filter ReplaceTokens, tokens: [
        	"PROJECT_NAME": "Topographica",
        	"PROJECT_SLUG": "topographica",
            "VERSION": version,
            "DATE": new Date().format('yyyy-MM-dd')
        ]
    }

    dependencies {
        testCompile 'junit:junit:4.12'
    }

}

// Web server (library, used by plugin and by standalone web server)
project(':topographica-web-shared') {
    dependencies {
        compileOnly 'com.google.guava:guava:18.0'
        testCompile 'com.google.guava:guava:18.0'
        compileOnly 'io.netty:netty-all:4.1.19.Final'
        compileOnly 'com.googlecode.json-simple:json-simple:1.1.1'
        testCompile 'com.googlecode.json-simple:json-simple:1.1.1'
    }
}

// Web server (standalone executable)
project(':topographica-web-standalone') {
    apply plugin: 'com.github.johnrengelman.shadow'

    dependencies { // Must be included in JAR file
        compile 'com.google.guava:guava:18.0'
        compile 'io.netty:netty-all:4.1.19.Final'
        compile 'com.googlecode.json-simple:json-simple:1.1.1'
        compile project(':topographica-web-shared')
    }

    shadowJar {
        destinationDir = file("$rootDir")
        archiveName = "Topographica-Webserver.${extension}"

        manifest {
            attributes 'Main-Class': 'nl.rutgerkok.topographica.webserver.Main'
        }
    }

    build.dependsOn(shadowJar)
}

// Spigot plugin (main)
project(':topographica-spigot') {
    apply plugin: 'com.github.johnrengelman.shadow'

    dependencies {
        compileOnly 'org.spigotmc:spigot-api:1.13-pre7-R0.1-SNAPSHOT'
        testCompile 'org.spigotmc:spigot-api:1.13-pre7-R0.1-SNAPSHOT'
        testCompile 'com.google.guava:guava:18.0'
        compile project(':topographica-web-shared')
    }

    processResources { // config.yml and plugin.yml replacements
        filter ReplaceTokens, tokens: [
            "PROJECT_NAME": "Topographica",
            "VERSION": version,
            "DATE": new Date().format('yyyy-MM-dd')
        ]
    }

    shadowJar {
        dependencies {
            exclude(dependency('com.google.guava:guava'))
            exclude(dependency('io.netty:netty-all'))
            exclude(dependency('com.googlecode.json-simple:json-simple'))
        }
        destinationDir = file("$rootDir")
        archiveName = "Topographica.${extension}"
    }

    build.dependsOn(shadowJar)
}
